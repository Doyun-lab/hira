*최종 구축 테이블 - 
진료내역별 환자정보(성별, 연령대, 입원일수, 합병증 발생여부 - 같은 명세서상 제1,2부상병에 I60~I63이 있는지?
약품 주성분 코드 별 복용기간, 사용량);

*471,766건;
DATA T200_AGE;
   SET FS.NPS200;

   WHERE FOM_TP_CD IN ('021', '121', '031', '131') AND 
   (SUBSTR(MAIN_SICK, 1, 3) in ('I63','I67','O22','O87') OR SUBSTR(MAIN_SICK, 1, 3) in ('I60', 'I61', 'I62', 'I63'));

   IF 0<=PAT_AGE<=5 THEN AGG = 1;
   ELSE IF 6<=PAT_AGE<=10 THEN AGG = 2;
   ELSE IF 11<=PAT_AGE<=15 THEN AGG = 3;
   ELSE IF 16<=PAT_AGE<=20 THEN AGG = 4;
   ELSE IF 21<=PAT_AGE<=25THEN AGG = 5;
   ELSE IF 26<=PAT_AGE<=30THEN AGG = 6;
   ELSE IF 31<=PAT_AGE<=35 THEN AGG = 7;
   ELSE IF 36<=PAT_AGE<=40THEN AGG = 8;
   ELSE IF 41<=PAT_AGE<=45 THEN AGG = 9;
   ELSE IF 46<=PAT_AGE<=50 THEN AGG = 10;
   ELSE IF 51<=PAT_AGE<=55 THEN AGG = 11;
   ELSE IF 56<=PAT_AGE<=60 THEN AGG = 12;
   ELSE IF 61<=PAT_AGE<=65 THEN AGG = 13;
   ELSE IF 66<=PAT_AGE<=70 THEN AGG = 14;
   ELSE IF 71<=PAT_AGE<=75 THEN AGG = 15;
   ELSE IF 76<=PAT_AGE<=80 THEN AGG = 16;
   ELSE IF 81<=PAT_AGE THEN AGG=17;
RUN;

*397,222건;
DATA T300_GNL_CD;
SET FS.NPS300;
WHERE GNL_CD NOT IN('$') AND
GNL_CD IN('110701ATB','110702ATB','110801ATB','110802ATB','111001ACE','111001ATB','111001ATE',
'111002ATE','111003ACE','111003ATE','133201ACR','133201ATB','133202ATB','136901ATB','140201BIJ',
'140202BIJ','140203BIJ','152101BIJ','152102BIJ','152103BIJ','152104BIJ','152105BIJ','168601BIJ','168602BIJ',
'198401BIJ','198402BIJ','198403BIJ','198407BIJ','239201ATB','239202ATB','244101ACE','244101ACH','244102ACH',
'249103ATB','249105ATB','256800ATB','359601BIJ','359602BIJ','359603BIJ','492501ATB','498801ATB','498900ATB',
'501501ATB','506100ATB','511401ATB','511402ATB','511403ATB','517900ACH','613701ACH','613702ACH','617001ATB','617002ATB');
RUN;

*주상병이 4개인 명세서 추출(환자정보);
PROC SQL;
CREATE TABLE TEMP1 AS
SELECT DISTINCT JID, MID, SEX_TP_CD AS GENDER, AGG AS AGE, VST_DDCNT AS DAY
FROM T200_AGE 
WHERE FOM_TP_CD IN ('021', '121') AND SUBSTR(MAIN_SICK,1,3) in ('I63','I67','O22','O87')
ORDER BY JID, MID;
QUIT;

PROC SQL;
CREATE TABLE TEMP2_1 AS
SELECT DISTINCT MID, GNL_CD, 1 AS GNL_CD_YN, SUM(TOT_INJC_DDCNT_EXEC_FQ) AS SUM_DDCNT
FROM T300_GNL_CD
GROUP BY MID, GNL_CD
ORDER BY MID, GNL_CD;
QUIT;
PROC SQL;
CREATE TABLE TEMP2_2 AS
SELECT DISTINCT MID, AVG(SUM_DDCNT) AS AVG_DDCNT
FROM TEMP2_1
GROUP BY MID 
ORDER BY MID;
QUIT;

PROC SQL;
CREATE TABLE TEMP3 AS
SELECT T1.*, T2.*
FROM TEMP1 T1 LEFT JOIN TEMP2_1 T2
ON T1.MID=T2.MID
ORDER BY MID, GENDER, AGE, DAY;
QUIT;

PROC TRANSPOSE DATA=TEMP3 OUT=TEMP4(DROP=_NAME_) PREFIX=DRUG_;
BY MID GENDER AGE DAY ;
ID GNL_CD;
VAR GNL_CD_YN;
RUN;

*주상병이 4개인 명세서의 부상병 중 합병증 여부 추출 - I63은 빼야 할 것 같은데?;
PROC SQL;
CREATE TABLE TEMP5 AS
SELECT DISTINCT MID, 1 AS HB_YN
FROM FS.NPS400
WHERE MID IN(SELECT DISTINCT MID FROM TEMP1) AND SICK_TY_CD NOT IN('3') AND SICK_SNO>1 AND 
SUBSTR(SICK_CD, 2, 3) in ('I60', 'I61', 'I62', 'I63');
QUIT;

PROC SQL;
CREATE TABLE TEMP6 AS
SELECT T1.*, T2.*, T3.*
FROM TEMP4 T1 
LEFT JOIN TEMP2_2 T2 ON T1.MID=T2.MID
LEFT JOIN TEMP5 T3 ON T1.MID=T3.MID
ORDER BY T1.MID;
QUIT;

PROC CONTENTS DATA=TEMP6 VARNUM;
RUN;

PROC SQL;
CREATE TABLE FINAL AS
SELECT
MONOTONIC() AS NO, GENDER, AGE, DAY, HB_YN, AVG_DDCNT, 
DRUG_110701ATB,DRUG_110702ATB,DRUG_110801ATB,DRUG_110802ATB,DRUG_111001ACE,DRUG_111001ATE,
DRUG_133201ACR,DRUG_133201ATB,DRUG_133202ATB,DRUG_136901ATB,DRUG_140201BIJ,DRUG_140202BIJ,
DRUG_140203BIJ,DRUG_152102BIJ,DRUG_152103BIJ,DRUG_152104BIJ,DRUG_152105BIJ,DRUG_168601BIJ,
DRUG_168602BIJ,DRUG_198402BIJ,DRUG_198403BIJ,DRUG_239201ATB,DRUG_239202ATB,DRUG_244101ACH,
DRUG_244102ACH,DRUG_249103ATB,DRUG_249105ATB,DRUG_359601BIJ,DRUG_492501ATB,DRUG_498801ATB,
DRUG_498900ATB,DRUG_501501ATB,DRUG_506100ATB,DRUG_511401ATB,DRUG_511402ATB,DRUG_511403ATB,
DRUG_517900ACH,DRUG_613701ACH,DRUG_613702ACH,DRUG_617001ATB,DRUG_617002ATB
FROM TEMP6;
QUIT;









